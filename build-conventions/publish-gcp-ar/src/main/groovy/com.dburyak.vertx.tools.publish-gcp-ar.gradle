plugins {
    id 'maven-publish'
    id 'com.google.cloud.artifactregistry.gradle-plugin' apply false
}

java {
    withJavadocJar()
    withSourcesJar()
}

javadoc {
    options.addBooleanOption('html5', true)
}

publishing {
    publications {
        library(MavenPublication) {
            from components.java
            versionMapping {
                usage(Usage.JAVA_API) {
                    fromResolutionOf('runtimeClasspath')
                }
                usage(Usage.JAVA_RUNTIME) {
                    fromResolutionResult()
                }
            }
        }
    }

    repositories {
        mavenLocal()
    }
}

def enabled = findProperty('publishToGcpArtifactRegistry') ?: System.getenv('GCP_AR_PUBLISH_ENABLED')?.toBoolean()
def location = findProperty('gcpArtifactRegistryLocation') ?: System.getenv('GCP_AR_LOCATION')
def gcpProjectId = findProperty('gcpArtifactRegistryProjectId') ?: System.getenv('GCP_AR_GCP_PROJECT_ID')
def repoName = findProperty('gcpArtifactRegistryRepoName') ?: System.getenv('GCP_AR_REPOSITORY_NAME')

if (enabled) {
    if (!location || !gcpProjectId || !repoName) {
        throw new GradleException('location, projectId and repositoryName values must be set to ' +
                'publish to GCP Artifact Registry')
    }

    project.plugins.apply 'com.google.cloud.artifactregistry.gradle-plugin'

    publishing {
        repositories {
            mavenLocal()
            maven { url "artifactregistry://$location-maven.pkg.dev/$gcpProjectId/$repoName" }
        }
    }
}
