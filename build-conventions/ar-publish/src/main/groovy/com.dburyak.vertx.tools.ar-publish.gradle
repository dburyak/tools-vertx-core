plugins {
    id 'maven-publish' apply false
    id 'com.google.cloud.artifactregistry.gradle-plugin' apply false
}

if (project.findProperty('publish.ar') == true) {
    println 'applying ar-publish plugin'
    apply plugin: 'maven-publish'
    apply plugin: 'com.google.cloud.artifactregistry.gradle-plugin'

    java {
        withJavadocJar()
        withSourcesJar()
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }

        repositories {
            maven {
                def (location, gcpProjectId, repoName) = [
                        System.getenv('AR_LOCATION'),
                        System.getenv('AR_GCP_PROJECT_ID'),
                        System.getenv('AR_REPOSITORY_NAME')
                ]
                if (!location || !gcpProjectId || !repoName) {
                    throw new GradleException('AR_LOCATION, AR_GCP_PROJECT_ID and AR_REPOSITORY_NAME env vars ' +
                            'must be set')
                }
                url "artifactregistry://$location-maven.pkg.dev/$gcpProjectId/$repoName"
            }
        }
    }

}
