plugins {
    id 'java'
    id 'groovy'
    id 'com.dburyak.vertx.tools.dependency-management'
    id 'io.micronaut.minimal.library'
    id 'com.coditory.integration-test'
    id 'jacoco'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

dependencyLocking {
    lockAllConfigurations()
}

repositories {
    mavenCentral()
}

configurations {
    testAnnotationProcessor { extendsFrom annotationProcessor }
    integrationAnnotationProcessor { extendsFrom testAnnotationProcessor }
    compileOnly { extendsFrom annotationProcessor }
    testCompileOnly { extendsFrom compileOnly, testAnnotationProcessor }
    integrationCompileOnly { extendsFrom testCompileOnly, integrationAnnotationProcessor }
}

micronaut {
    importMicronautPlatform = false
}

dependencies {
    // lombok
    annotationProcessor 'org.projectlombok:lombok'

    // vertx
    implementation 'io.vertx:vertx-core'
    implementation 'io.vertx:vertx-rx-java3'

    // micronaut
    implementation 'io.micronaut:micronaut-aop'
    implementation 'io.micronaut.validation:micronaut-validation'
    annotationProcessor 'io.micronaut:micronaut-inject-java'
    annotationProcessor 'io.micronaut.validation:micronaut-validation-processor'

    // logging
    implementation 'org.slf4j:slf4j-api'
    testImplementation 'org.apache.logging.log4j:log4j-core'
    testImplementation 'org.apache.logging.log4j:log4j-slf4j-impl'

    // testing
    testImplementation 'io.vertx:vertx-junit5'
    testImplementation 'org.assertj:assertj-core'
    testImplementation 'org.apache.groovy:groovy-all'
    testImplementation 'org.spockframework:spock-core'
    testRuntimeOnly 'net.bytebuddy:byte-buddy'
    testRuntimeOnly 'org.objenesis:objenesis'
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    executionData(fileTree(project.layout.buildDirectory).include("jacoco/*.exec"))
    reports {
        html.required = true
        xml.required = true
    }
}

tasks.register('printRuntimeClasspath') {
    doLast {
        configurations.runtimeClasspath.each {
            println it
        }
    }
}

tasks.register('printCompileClasspath') {
    doLast {
        configurations.compileClasspath.each {
            println it
        }
    }
}
