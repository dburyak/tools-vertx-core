plugins {
    id 'java-platform'
}

javaPlatform {
    allowDependencies()
}

dependencies {
    // micronautVersion is defined in gradle.properties (in repo root)
    api platform("io.micronaut.platform:micronaut-platform:$micronautVersion")
    api platform('com.fasterxml.jackson:jackson-bom:[2.15, 3)!!2.15.2')
    api platform('org.apache.logging.log4j:log4j-bom:[2.20, 3)!!2.20.0')
    api platform('org.assertj:assertj-bom:[3.24, 4)!!3.24.2')
    api platform('org.spockframework:spock-bom:2.4-M1-groovy-4.0')
    api platform('com.google.cloud:libraries-bom:[26.22, 27)!!26.22.0')

    constraints {

        // vertx
        def vertxVersion = '[4, 5)!!4.5.1'
        api "io.vertx:vertx-core:$vertxVersion"
        api "io.vertx:vertx-rx-java3:$vertxVersion"
        api "io.vertx:vertx-lang-groovy:$vertxVersion"
        api "io.vertx:vertx-junit5:$vertxVersion"
        api "io.vertx:vertx-hazelcast:$vertxVersion"
        api "io.vertx:vertx-config:$vertxVersion"

        api 'org.projectlombok:lombok:[1.18.30, 1.19)!!1.18.30'
        api 'org.apache.groovy:groovy-all:[4.0, 5)!!4.0.14'
        def slf4jVersion = '[2, 3)!!2.0.7'
        api "org.slf4j:slf4j-api:$slf4jVersion"
        api "org.slf4j:slf4j-nop:$slf4jVersion"
        api 'com.esotericsoftware:kryo:[5.5, 6)!!5.5.0'

        api 'net.bytebuddy:byte-buddy:[1.14, 2)!!1.14.7'
        api 'org.objenesis:objenesis:[3.3, 4)!!3.3'
    }
}
