plugins {
    id 'java'
    id 'groovy'
    id 'io.micronaut.minimal.library'
    id 'com.coditory.integration-test'
    id 'jacoco'
}

group = 'com.dburyak.vertx.tools'

repositories {
    mavenLocal()
    mavenCentral()
}

configurations {
    testAnnotationProcessor { extendsFrom annotationProcessor }
    integrationAnnotationProcessor { extendsFrom testAnnotationProcessor }
    compileOnly { extendsFrom annotationProcessor }
    testCompileOnly { extendsFrom compileOnly, testAnnotationProcessor }
    integrationCompileOnly { extendsFrom testCompileOnly, integrationAnnotationProcessor }
}

micronaut {
    // this must match the version of micronaut dependencies defined in build-conventions/versions.gradle
    // there's no easy way to have it defined in a single place
    version '[3.8, 4)!!3.8.5'
}

dependencies {
    implementation platform(project(':build-conventions:versions'))
    annotationProcessor platform(project(':build-conventions:versions'))

    // lombok
    annotationProcessor 'org.projectlombok:lombok'

    // vertx
    implementation 'io.vertx:vertx-core'
    implementation 'io.vertx:vertx-rx-java3'

    // micronaut
    implementation 'io.micronaut:micronaut-aop'
    implementation 'io.micronaut:micronaut-validation'
    annotationProcessor 'io.micronaut:micronaut-inject'
    annotationProcessor 'io.micronaut:micronaut-validation'

    // logging
    implementation 'org.slf4j:slf4j-api'
    testImplementation 'org.apache.logging.log4j:log4j-core'
    testImplementation 'org.apache.logging.log4j:log4j-slf4j-impl'

    // testing
    testImplementation 'io.vertx:vertx-junit5'
    testImplementation 'org.assertj:assertj-core'
    testImplementation 'org.apache.groovy:groovy-all'
    testImplementation 'org.spockframework:spock-core'
    testRuntimeOnly 'net.bytebuddy:byte-buddy'
    testRuntimeOnly 'org.objenesis:objenesis'
}

tasks.withType(Test) {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    executionData(fileTree(project.buildDir).include("jacoco/*.exec"))
    reports {
        html.required = true
    }
}

task printRuntimeClasspath {
    doLast {
        configurations.runtimeClasspath.each {
            println it
        }
    }
}

task printCompileClasspath {
    doLast {
        configurations.compileClasspath.each {
            println it
        }
    }
}
